apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                from components.kotlin
//                if (project.plugins.findPlugin("com.android.library")) {
//                    println("Components: " + components*.name)
//                }
//                if (project.plugins.findPlugin("com.android.library")) {
//                    from components.release
//                } else {
//                    from components.java
//                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Generate strings (fractional indices) that can be used as sorting keys.'
                    url = 'https://github.com/darvelo/fractional-indexing-kotlin'
                    licenses {
                        license {
                            name = 'Creative Commons Zero v1.0 Universal'
                            url = 'https://github.com/darvelo/fractional-indexing-kotlin/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'darvelo'
                            name = 'David Arvelo'
                            email = 'david@davidarvelo.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/darvelo/fractional-indexing-kotlin.git'
                        developerConnection = 'scm:git:ssh://github.com/darvelo/fractional-indexing-kotlin.git'
                        url = 'https://github.com/darvelo/fractional-indexing-kotlin/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}